plugins {
	id 'org.springframework.boot' version '3.0.2'
	id 'io.spring.dependency-management' version '1.1.0'
	id "com.google.cloud.tools.jib" version "3.3.1"
	id 'java'
	id 'maven-publish'
}

group = 'eu.thijslemmens.mqtt'
version = '0.0.2-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/release' }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.integration:spring-integration-mqtt'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.integration:spring-integration-jdbc'
	implementation 'org.flywaydb:flyway-core'

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

	runtimeOnly group: 'org.postgresql', name: 'postgresql', version: '42.5.1'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.testcontainers:postgresql:1.17.6'
}

tasks.named('test') {
	useJUnitPlatform()
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/thijslemmens/mqtt-to-pg")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			artifact bootJar
			artifactId bootJar.baseName
		}
	}
}

jib {
	from {
		platforms {
			platform {
				architecture = 'arm64'
				os = 'linux'
			}
			platform {
				architecture = 'amd64'
				os = 'linux'
			}
		}
	}
	to {
		image = project.property('image')
	}
}